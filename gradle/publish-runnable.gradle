apply plugin: 'maven-publish'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'com.jfrog.bintray'

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

shadowJar {
    manifest {
        attributes 'Main-Class': project.ext.get("mainClass")
    }
    mergeServiceFiles()
}

publishing {
    publications {
        source(MavenPublication) {
            from components.java
            artifact(sourceJar) {
                classifier 'sources'
            }
        }
        shadow(MavenPublication) {
            artifact(shadowJar)
        }
    }
}

bintray {
    user = System.env.BINTRAY_USER
    key = System.env.BINTRAY_KEY
    publish = true
    pkg {
        repo = 'releases'
        name = 'groovy-language-server'
        userOrg = 'palantir'
        licenses = ['Apache-2.0']
        publications = ['sourceJarPublication', 'shadowJarPublication']
    }
}

bintrayUpload.onlyIf {
    project.version ==~ /\d+\.\d+\.\d+(-rc[0-9]+)?/
}

bintrayUpload.dependsOn { generatePomFileForSourcePublication }
bintrayUpload.dependsOn { generatePomFileForShadowPublication }
bintrayUpload.dependsOn { shadowJar }
bintrayUpload.dependsOn { sourceJar }
bintrayUpload.dependsOn { build }
